module AutocorrectTest where

import Data.List
import qualified Data.Map.Strict as Map
import GHC.Arr
import Handler.Autocorrect
import Test.HUnit (Counts, Test (..), runTestTT, (~?=))
import Test.QuickCheck (Testable (..))
import qualified Test.QuickCheck as QC
import Prelude

testDict1 :: Map.Map String Int
testDict1 =
  Map.fromList
    [ ("haskell", 5),
      ("python", 2),
      ("java", 3)
    ]

testDict2 :: Map.Map String Int
testDict2 = Map.empty

testDict3 :: Map.Map String Int
testDict3 =
  Map.fromList
    [ ("haskell", 5),
      ("python", 2),
      ("java", 3),
      ("spelling", 1),
      ("corrected", 10),
      ("bicycle", 1),
      ("inconvenient", 1),
      ("arranged", 1),
      ("poetry", 1),
      ("word", 1)
    ]

-- | Test for the most probable spelling correction for @word@.
testCorrection :: Test
testCorrection =
  TestList
    [ correction testDict3 "speling" ~?= "spelling", -- insert
      correction testDict3 "korrectud" ~?= "corrected", -- replace 2
      correction testDict3 "bycycle" ~?= "bicycle", -- replace
      correction testDict3 "inconvient" ~?= "inconvenient", -- insert 2
      correction testDict3 "arrainged" ~?= "arranged", -- delete
      correction testDict3 "peotry" ~?= "poetry", -- transpose
      correction testDict3 "peotryy" ~?= "poetry", -- transpose + delete
      correction testDict3 "word" ~?= "word", -- known
      correction testDict3 "quintessential" ~?= "quintessential", -- unknown
      correction testDict3 "eptf" ~?= "word" -- shift1
    ]

-- | Test for probability
testP :: Test
testP =
  TestList
    [ p testDict1 "quintessential" ~?= 0
    ]

testNoDupCandidates :: Test
testNoDupCandidates =
  TestList
    [ noDupCandidates ["test", "test", "test1"] ~?= ["test", "test1"]
    ]

testKnown :: Test
testKnown =
  TestList
    [ known testDict1 [] ~?= [],
      known testDict1 ["haskell", "python"] ~?= ["haskell", "python"]
    ]

testCandidates :: Test
testCandidates =
  TestList
    [ candidates testDict1 "haskel" ~?= ["haskell", "haskell"],
      candidates testDict2 "haskell" ~?= ["haskell"],
      candidates testDict3 "haskell" ~?= ["haskell"]
    ]

testEdits1 :: Test
testEdits1 =
  TestList
    [ length (edits1 "") ~?= 26,
      length (edits1 "a") ~?= 79,
      length (edits1 "somthing") ~?= 450,
      length (noDupCandidates (edits1 "somthing")) ~?= 435
    ]

testEdits2 :: Test
testEdits2 =
  TestList
    [ length (edits2 "s") ~?= 8944,
      length (edits2 "ss") ~?= 21452,
      length (edits2 "sa") ~?= 21452,
      length (noDupCandidates (edits2 "ss")) ~?= 6505,
      length (noDupCandidates (edits2 "sa")) ~?= 7130
    ]

-- an empty map should return the same word
prop_correction :: String -> Bool
prop_correction word = correction Map.empty word == word

-- quickcheck for candidates to
prop_candidates :: String -> Bool
prop_candidates word = length (candidates Map.empty word) == 1

-- quickcheck for known
prop_known :: String -> Bool
prop_known word = null (known Map.empty [word])

-- quickcheck for edits1
prop_edits1 :: String -> Bool
prop_edits1 word = length (edits1 word) == 26 * (length word * 2 + 1) + length word

-- helper function for checking all elements in the list generated by edits1 is valid
-- calculate the edit distance between two strings
editDistance :: Eq a => [a] -> [a] -> Int
editDistance xs ys = table ! (a, b)
  where
    (a, b) = (length xs, length ys)
    x = array (1, a) (zip [1 ..] xs)
    y = array (1, b) (zip [1 ..] ys)

    table :: Array (Int, Int) Int
    table = array bn [(ij, distance ij) | ij <- range bn]
      where
        bn = ((0, 0), (a, b))

    distance :: (Int, Int) -> Int
    distance (0, j) = j
    distance (i, 0) = i
    distance (i, j) =
      minimum
        [ table ! (i - 1, j) + 1,
          table ! (i, j - 1) + 1,
          if x ! i == y ! j then table ! (i - 1, j - 1) else 1 + table ! (i - 1, j - 1)
        ]

-- helper function
-- apply the above editDistance function to all elements in a list and return a list of integers
-- if the list is empty, return an empty list
editDistanceList :: Eq a => [[a]] -> [a] -> [Int]
editDistanceList [] _ = []
editDistanceList (x : xs) word = editDistance x word : editDistanceList xs word

-- quickcheck for all the words generated by edits1 has edit distance 1 from the original word
-- we use <= not == because the original word is also included in our edit1 list
prop_edits1_distance :: String -> Bool
prop_edits1_distance word = all (\x -> editDistance word x <= 1) (edits1 word)

-- quickcheck for all the words generated by edits2 has edit distance 2 from the original word
prop_edits2_distance :: String -> Bool
prop_edits2_distance word = all (\x -> editDistance word x <= 2) (edits2 word)

quickCheckN :: (Testable prop) => Int -> prop -> IO ()
quickCheckN n = QC.quickCheck . QC.withMaxSuccess n

-------------------------- all properties and tests in this module  -----------------------------
test_all :: IO Counts
test_all =
  runTestTT $
    TestList
      [ testCorrection,
        testP,
        testNoDupCandidates,
        testKnown,
        testCandidates,
        testEdits1,
        testEdits2
      ]

qc :: IO ()
qc = do
  putStrLn "prop_correction"
  quickCheckN 100 prop_correction
  putStrLn "prop_candidates"
  quickCheckN 100 prop_candidates
  putStrLn "prop_known"
  quickCheckN 100 prop_known
  putStrLn "prop_edits1"
  quickCheckN 100 prop_edits1
  putStrLn "prop_edits1_distance"
  quickCheckN 100 prop_edits1_distance -- extremely slow, around 10 min
  putStrLn "prop_edits2_distance"
  quickCheckN 100 prop_edits2_distance -- around 80 min, only for testing
